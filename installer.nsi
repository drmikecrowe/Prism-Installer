; Customizations:
; 1) Allow editing of the URL by uncommenting the "Page custom PromptUrlPage" line.  NOTE:  Remove the 3 assignment lines in MainSection per note
; 2) You can create Start Menu items in the -AdditionalIcons section (make sure to uncomment in uninstall too)

!define CONFIG_URL "http://www.google.com"    ; ENTER YOUR DESIRED URL HERE
!define CONFIG_APP "MYAPP@prism.app"          ; ENTER YOUR Application ID here (pseudo-email addres)
!define CONFIG_APP_NAME "MYPRISMAPP"          ; ENTER YOUR Application Name here 

; Script generated by the HM NIS Edit Script Wizard.
!define PRISM_VERSION "1.0b3"
!define INSTALL_VERSION "v0.6"
!define FULL_VERSION "${PRISM_VERSION}-${INSTALL_VERSION}"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Prism"
!define PRODUCT_VERSION "${PRISM_VERSION}"
!define PRODUCT_PUBLISHER "Mozilla Labs Prism"
!define PRODUCT_WEB_SITE "http://prism.mozillalabs.com/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\prism.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "ICONS\logo_prism.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "LICENSE"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY

; UNCOMMENT THIS LINE IF YOU WANT THE INSTALLER TO PROMPT/EDIT THE URL
;Page custom PromptUrlPage


; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

Var Dialog
Var Label1
Var Label2
Var Text
Var Url
Var Application
Var ApplicationName

!include "nsDialogs.nsh"

Function PromptUrlPage
  StrCpy $Url ${CONFIG_URL}
  StrCpy $Application ${CONFIG_APP}
  StrCpy $ApplicationName ${CONFIG_APP_NAME}

  nsDialogs::Create 1018
  Pop $Dialog
  ${If} $Dialog == error
    Abort
  ${EndIf}
  ${NSD_CreateLabel} 0 0 100% 36u "By default, Prism uses the $Url as the URL.  You may change it, if so desired."
  Pop $Label1
  ${NSD_CreateLabel} 0 38u 10% 8u "Prism URL:"
  Pop $Label2
  ${NSD_CreateText} 40u 36u 70% 12u $Url
  Pop $Text
  ${NSD_OnChange} $Text PromptUrlPageTextChange
  nsDialogs::Show
FunctionEnd

Function PromptUrlPageTextChange
  ${NSD_GetText} $Text $Url
FunctionEnd

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "PrismSetup-${FULL_VERSION}.exe"
InstallDir "$PROGRAMFILES\Prism"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" sec01
  ; NOTE: Remove these 3 lines if you us the PromptUrlPage dialog
  StrCpy $Url ${CONFIG_URL}
  StrCpy $Application ${CONFIG_APP}
  StrCpy $ApplicationName ${CONFIG_APP_NAME}
  
  SetOutPath "$INSTDIR"
  SetOverwrite try
  File "icons\logo_prism.ico"

  ;  PRISM application data is recursively copied from yourAppPgmFiles to "$APPDATA\yourApp\Prism"
  SetOutPath "$INSTDIR\"
  SetOverwrite ifnewer
  File /r "prism.${PRISM_VERSION}.en-US.win32\prism\*"

  ;  PRISM webApp data is recursively copied from yourAppAppData to "$APPDATA\WebApps"
  SetOutPath "$APPDATA\WebApps\$Application\"
  File /r "webapps\*"

  ;  Write the webapp.ini file
  ClearErrors
  FileOpen $0 "$APPDATA\WebApps\$Application\webapp.ini" w
  IfErrors abort
  FileWrite $0 "[Parameters]$\r$\n"
  FileWrite $0 "id=$Application$\r$\n"
  FileWrite $0 "name=$ApplicationName$\r$\n"
  FileWrite $0 "uri=$Url$\r$\n"
  FileWrite $0 "icon=webapp$\r$\n"
  FileWrite $0 "status=true$\r$\n"
  FileWrite $0 "location=false$\r$\n"
  FileWrite $0 "sidebar=false$\r$\n"
  FileWrite $0 "navigation=true$\r$\n"
  FileWrite $0 "trayicon=false$\r$\n"
  FileClose $0
  Goto done

  abort:
  Abort

  done:

  ;  set the context for $SMPROGRAMS and other shell folder to the 'current' user
  CreateShortcut "$DESKTOP\${PRODUCT_NAME}.lnk" "$INSTDIR\prism.exe" "-override $\"$APPDATA\WebApps\$Application\override.ini$\" -webapp $Application" "$INSTDIR\logo_prism.ico"

SectionEnd

Section -AdditionalIcons
  ;WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  ;CreateDirectory "$SMPROGRAMS\$ApplicationName"
  ;CreateShortCut "$SMPROGRAMS\$ApplicationName\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  ;CreateShortCut "$SMPROGRAMS\$ApplicationName\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKCU "Software\${PRODUCT_NAME}" "InstallDir" "$INSTDIR"
  ;WriteRegStr HKCU "Software\${PRODUCT_NAME}" "ProgramDir" "$SMPROGRAMS\GTDInbox"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\prism.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\prism.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?  NOTE: This includes completely removing Prism." IDYES +2
  Abort
FunctionEnd

Section Uninstall
  ReadRegStr $INSTDIR HKCU "Software\${PRODUCT_NAME}" "InstallDir"
  ReadRegStr $Text HKCU "Software\${PRODUCT_NAME}" "InstallDir"

  RMDir /r "$INSTDIR\*.*"
  RMDir /r "$Text\*.*"
  
  Delete "$DESKTOP\${PRODUCT_NAME}.lnk"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKCU "Software\${PRODUCT_NAME}"
  SetAutoClose true
SectionEnd